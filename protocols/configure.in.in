AC_DEFUN([AC_LIBGADU_DEFINE],
[
sed -e "s/#undef __GG_LIBGADU_$1/#define __GG_LIBGADU_$1/" \
${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h > ${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h.1
mv ${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h.1 ${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h
])
	 
LIBGG_INCLUDES=""
LIBGG_LIBS=""
ac_libgadu_includes=""
ac_libgadu_libs=""

AC_ARG_WITH(external-libgadu,
  [AC_HELP_STRING(--with-external-libgadu,
    [use external libgadu library @<:@default=check@:>@])],
  [], with_external_libgadu=check)

AC_ARG_WITH(libgadu-includes,
	AC_HELP_STRING([--with-libgadu-includes=DIR], [where the libgadu includes are.]),
	[  ac_libgadu_includes="$withval" ])

if test "$ac_libgadu_includes" != "" ; then
LIBGG_INCLUDES="-I$ac_libgadu_includes"
fi

AC_ARG_WITH(libgadu-libs,
	AC_HELP_STRING([--with-libgadu-libs=DIR], [where the libgadu libraries are.]),
	[  ac_libgadu_libs="$withval" ])

if test "$ac_libgadu_libs" != "" ; then
	LIBGG_LIBS="-L$ac_libgadu_libs"
fi

if test "x$with_external_libgadu" != xno; then
	ac_save_LIBS="$LIBS"
	ac_save_CFLAGS="$CFLAGS"
	LIBS="$LIBGG_LIBS -lgadu $LIBPTHREAD"
	CFLAGS="$CFLAGS $LIBGG_INCLUDES"
	AC_MSG_CHECKING([libgadu version 1.5(rcX) with pthread support])
	AC_TRY_RUN(
	[
	
	#include <libgadu.h>
	#include <stdio.h>
	#include <string.h>
		
	int main()
	{
#if defined __GG_LIBGADU_HAVE_PTHREAD && defined GG_LOGIN60
		int maj, min, date;
		sscanf( gg_libgadu_version(), "%u.%u.%u", &maj,&min,&date );
		if ( maj != 1 ) {
			return 1;
		}
		if ( ( min == 4 || min == 5 ) && date < 20040520 ) {
			return 1;
		}

		if ( min == 5 ){
			return 0;
		}
			     
#endif
	    return 1;
	}
	], [
	LIBGG_LIBS="$LIBGG_LIBS -lgadu $LIBPTHREAD"
	AC_MSG_RESULT([yes])
	COMPILE_GADU=true
	use_libgadu_copy=
	], [
	AC_MSG_RESULT([no])
	])
	LIBS="$ac_save_LIBS"
	CFLAGS="$ac_save_CFLAGS"

	if test "x$with_external_libgadu" != xcheck && test -z "$COMPILE_GADU"; then
		AC_MSG_ERROR([--with-external-libgadu was given, but test for libgadu failed])
	fi
fi

if test -z "$COMPILE_GADU"; then
	AC_MSG_CHECKING([if supplied libgadu-copy can be used])
	if test "$kde_use_threading" = "yes"; then
		AC_MSG_RESULT([yes])
		use_libgadu_copy=yes
		COMPILE_GADU=true
	else
		AC_MSG_RESULT([no (no pthread), support for Gadu-Gadu will be disabled])
		use_libgadu_copy=
		COMPILE_GADU=
	fi
fi

AC_SUBST(LIBGG_INCLUDES)
AC_SUBST(LIBGG_LIBS)
AC_SUBST(COMPILE_GADU)
AM_CONDITIONAL(include_gadu, test -n "$COMPILE_GADU")
AM_CONDITIONAL(include_libggcopy, test -n "$use_libgadu_copy")

if test "$use_libgadu_copy" = "yes"; then
	cp ${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h.in \
	${srcdir}/kopete/protocols/gadu/libgadu/libgadu-config.h
	if test "$ac_cv_c_bigendian" = "yes"; then
		AC_LIBGADU_DEFINE([BIGENDIAN])	
	fi
	KDE_CHECK_LONG_LONG()
	if test "$kde_cv_c_long_long" = "yes"; then
	AC_LIBGADU_DEFINE([HAVE_LONG_LONG])
	fi
	KDE_CHECK_SSL()
	if test  "$have_ssl" = "yes"; then
	AC_LIBGADU_DEFINE([HAVE_OPENSSL])
	fi
	AC_MSG_CHECKING([for C99-compatible vsnprintf()])
	AC_TRY_RUN(
	[
		#include <stdio.h>
		int main()
		{
			char tmp;
			return (snprintf(&tmp, sizeof(tmp), "test") != 4);
		}
	],[
		AC_MSG_RESULT([yes])
		AC_LIBGADU_DEFINE([HAVE_C99_VSNPRINTF])
	], [
		 AC_MSG_RESULT([no])
	])
	AC_CHECK_FUNCS([va_copy],
		       [AC_LIBGADU_DEFINE([HAVE_VA_COPY])],[])
	AC_CHECK_FUNCS([_va_copy],
		       [AC_LIBGADU_DEFINE([HAVE__VA_COPY])],[])
fi


AC_ARG_WITH(libidn, AC_HELP_STRING([--with-libidn=DIR],
            [Support IDN (needs GNU Libidn)]),
            libidn=$withval, libidn=yes)

if test "$libidn" != "no"; then
	if test "$libidn" != "yes"; then
		IDN_LDFLAGS="${LDFLAGS} -L$libidn/lib"
		IDN_CPPFLAGS="${CPPFLAGS} -I$libidn/include"
	fi
fi

KDE_CHECK_HEADER(idna.h,, libidn=no)

if test "$libidn" != "no" ; then
	KDE_CHECK_LIB(idn, stringprep_check_version,
		[libidn=yes; IDN_LIBS="-lidn"], libidn=no)
fi

if test "$libidn" != "no" ; then
	AC_DEFINE(LIBIDN, 1, [Define to 1 if you want IDN support.])
else
	AC_MSG_WARN([Libidn not found, Kopete Jabber plugin will not be compiled])
fi

AC_MSG_CHECKING([if Libidn can be used])
AC_MSG_RESULT($libidn)

AM_CONDITIONAL(include_jabber, test "$libidn" = "yes")

AC_SUBST(IDN_LDFLAGS)
AC_SUBST(IDN_CPPFLAGS)
AC_SUBST(IDN_LIBS)

#
# Sametime support
#

# lower and upper-bound versions of Meanwhile library
m4_define(meanwhile_version_min,        0.4.2)
m4_define(meanwhile_version_max,        0.5.0)

# Let the user disable the plugin
AC_ARG_ENABLE(sametime-plugin,
      AC_HELP_STRING([--enable-sametime-plugin], [build the Kopete Sametime plugin @<:@default=no@:>@]),
      [compile_meanwhile=$enableval],
      [compile_meanwhile=no]
      )

# Check and setup for Meanwhile library
KDE_PKG_CHECK_MODULES(MEANWHILE,
      [meanwhile >= meanwhile_version_min meanwhile < meanwhile_version_max],
      [have_meanwhile=yes],
      [have_meanwhile=no]
      )
AC_SUBST(MEANWHILE_CFLAGS)
AC_SUBST(MEANWHILE_LIBS)

# Check and setup for GLib library
KDE_PKG_CHECK_MODULES(GLIB,
      [glib-2.0 >= 2.0.0],
      [have_glib=yes],
      [have_glib=no]
      )
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

# Do we everything we need to compile the plugin
if test "x$have_meanwhile" != "xyes" && test "x$have_glib" != "xyes"; then
    compile_meanwhile=no
fi

# Let the user know
AC_MSG_CHECKING([if Sametime Plugin should be compiled])
AC_MSG_RESULT($compile_meanwhile)

# Here we go
AM_CONDITIONAL(include_meanwhile, [test "x$compile_meanwhile" = "xyes"])

#
# End of Sametime support
#

# testbed protocol
dnl define the configure option that disables testbed protocol
AC_ARG_ENABLE(testbed, [  --disable-testbed     disable kopete testbed protocol compilation ],  with_testbed=$enableval, with_testbed=yes)
AM_CONDITIONAL(include_testbed, test "$with_testbed" = "yes")
